#!/usr/bin/perl -w
use strict;
use FileHandle;

## Monitor output/*.out and *.err and report on state of system

## Open every file in the output diretctory and monitor the output.

## Encode here knowledge about what 120Proof programmes ouitput.  This is tightly linked.

$|++; #Flush
# use lib("$ENV{'Home120Proof'}/Perl");
# use One20Proof;

my $output_dir = "$ENV{'Home120Proof'}/output/";
my $o = OutputFilesState->new($output_dir);
$o->initialise();

my $lines_since_time = 0;
while(1){
    my $yoshimi_f = YoshimiOutFilter->new();
    my $yoshimi_e = YoshimiErrFilter->new();
    $o->update();
    my $output = $o->output();
    if($output){
	my $time = scalar(localtime());

	## Do some processing	
	my @output = split(/\n/, $output);
	foreach my $l (@output){
	    $lines_since_time++;
	    chomp $l;
	    if($l =~ /yoshimi\.out:(.+)/){
		print $yoshimi_f->process_line($l);
	    }elsif($l =~ /yoshimi\.err:(.+)/){
		print $yoshimi_e->process_line($l);
	    }else{
		print "$l\n";
	    }
	}
    }
    my $yoshimi_f_summary = $yoshimi_f->summary();
    my $yoshimi_e_summary = $yoshimi_e->summary();
    my $summary = $yoshimi_e_summary . $yoshimi_f_summary;

    if($summary){
	if($lines_since_time > 20){
	    print scalar(localtime())."\n";
	    $lines_since_time = 0;
	}
	print $summary;
    }
    sleep(.5);
}

exit;

## Yoshimi says too much...
package YoshimiOutFilter;

sub new {
    my $class = shift;
    my $self = {};
    return bless $self, $class;
}

sub summary {
    my $self = shift or die;
    my $summary = "";
    if($self->{xruns}){
	$summary .= $self->{xruns}->{count} . " xruns. ";
    }
    if($self->{sample_rate}){
	$summary .= "Sample rate: " .$self->{sample_rate} .". ";
    }
    if($self->{Instrument}){
	$summary .= "Instrument: $self->{Instrument}. ";
    }
    
    $summary and $summary = "Yoshimi: $summary\n";
    return $summary;
}
sub process_line {
    my $self = shift or die;
    my $l = shift or die;
    chomp $l;
    if($l =~ "xrun reported"){
	$self->{xruns} or $self->{xruns} = {};
	$self->{xruns}->{count} or $self->{xruns}->{count} = 0;
	$self->{xruns}->{count}++;
    }elsif($l =~ /Samplerate: (\d+)/){
	$self->{sample_rate} = $1;
    }elsif($l =~ /Instrument file .+\/([^\/]+) loaded$/){
	$self->{Instrument} = $1;
    }
    return "";
}

1;

## Yoshimi says too much...
package YoshimiErrFilter;

sub new {
    my $class = shift;
    my $self = {};
    $self->{errors} = [];
    
    return bless $self, $class;
}

sub summary {
    my $self = shift or die;
    my $summary = "";
    foreach my $error (@{$self->{errors}}){
	chomp $error;
	$summary .= "Yoshimi Error: $error\n";
    }
	
    return $summary;
}
sub process_line {
    my $self = shift or die;
    my $l = shift or die;
    chomp $l;
    if($l =~ /Interrupt/){
	my $result = "Yoshimi ";
	$self->{Instrument} and $result .= "Instrument: $self->{Instrument} ";
	$result .= "interrupted\n";
	return $result;
    }elsif($l =~ /Cannot read socket fd = (\d+) err = (.+)/){
	push(@{$self->{errors}}, "socket($1): $2");
    }
    return "";
}

1;


## To facilitate monitoring the strate of files in the output directory
## man perlobj
package OutputFilesState;
sub new {
    my $class = shift;

    ## The directory to monitor
    my $path = shift or die;
    -d $path or die "Pass the path to a directory";
    $path =~ /\/$/ or $path .= '/';
    my $self = {path => $path};
    
    return bless $self, $class;
}

sub initialise() {
    my $self = shift;

    ## Get a handle to the output directory
    opendir(my $dh, $self->{path}) or die "$!";
    $self->{dir} = $dh;

    ## Initialise cache
    $self->{cache} = {};

    
}

sub list() {
    my $self = shift;
    rewinddir($self->{dir});
    my @files = map{$self->{path}.$_}
    grep{/[^\.]/} ## Some non dod character
    grep {$_ !~ /\#/} ## Emacs writes these files temporarily
    readdir($self->{dir});
    return @files;
}

sub update() {
    my $self = shift;

    foreach my $fn ($self->list()){
	if(!-e $fn){
	    ## File no longer exists
	    $self->{cache}->{$fn} and $self->{cache}->{$fn} = undef;
	    next;
	}
	my $new_data = "";
	$self->{cache}->{$fn} or $self->{cache}->{$fn} = [0, ""];
	    
	open(my $fh, $fn) or die "$!: $fn";
	my @stat = stat($fh) or die "$!: $fn";
	my $sz = $stat[7]; ## Actual size
	my $csz = $self->{cache}->{$fn}->[0]; ## Cached size
	$self->{cache}->{$fn}->[0] = $sz;
	
	if($sz > $csz){
	    ## File size has grown.  Read the latest data
	    seek($fh, $csz, 0);

	    while(my $ln = <$fh>){
		$new_data .= $ln;
	    }
	}elsif($sz < $csz){
	    ## File has been restarted.  Read it all
	    seek($fh, 0, 0);

	    while(my $ln = <$fh>){
		$new_data .= $ln;
	    }
	}
	$self->{cache}->{$fn}->[1] = $new_data;
    }
}

sub output {
    my $self = shift;
    my $result = "";
    foreach my $fn (keys(%{$self->{cache}})){
	$fn =~ /([^\/]+)$/ or die "\$fn makes no sense: '$fn'";
	my $name = $1;
	my @output = split("\n", $self->{cache}->{$fn}->[1]);
	for my $l (@output){	    
	    $result .=  "$name: $l\n";
	}
    }
    return $result;
}
1;
